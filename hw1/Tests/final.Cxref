#!/usr/bin/perl -w

use strict;
my $debug = 0;

my $CLASS  = "223";
my $HWK    = "1";
my $NAME   = "Cxref";           # Name of program
my $TEST   = "tIJ.c";           # Name of test file (IJ is replaced by number)
my $ANSWER = "tIJ.X";           # Name of answer file (IJ is replaced by number)
my $DATE   = "02/07/20";        # Date script written
my $LANG   = "C";               # Language (C, Perl, ...)

# Blank-separated list of illegal files; wildcards permitted
my $hwkFiles = "mainBashful.* parsnip.* tokenize.*";

my $PROGRAM = "./$NAME";        # Name of executable

# Scale multi-part tests
my %SCALE;
%SCALE = ('27a' => .6, '27b' => .4,                 # Variations on char const
	  '30a' => .3, '30b' => .3, '30c' => .4,    # Spliced escape in string
	  '31a' => .3, '31b' => .3, '31c' => .4,    # Spliced escape in constant
	  '33a' => .3, '33b' => .3, '33c' => .4,    # Sequence of splices
	 );

my %WHICH;                      # Allow individual tests on command line
@WHICH{@ARGV}++
   if (@ARGV);

my @UNLINK;                                     # Files to delete on signal
$SIG{HUP} = $SIG{INT} = $SIG{QUIT}
   = $SIG{TERM} = \&cleanup;
my $TMPDIR = "/tmp/TEST.$NAME.$$";              # Name of temporary directory
sub cleanup {
   unlink (@UNLINK);                            # Delete files
   if (-e $TMPDIR) {                            # Delete temporary directory
      system ("/bin/chmod -R +w $TMPDIR");
      system ("/bin/rm -rf $TMPDIR");
   }
   exit;
}

$0 =~ s{^.*/}{};                                # Extract test type
my $TYPE = ($0 =~ m{^test\.}) ? "Public" : "Final";
print "\n$TYPE test script for $NAME ($DATE)\n\n";

&makeProgram
   unless ($LANG eq "Perl");

my $TimeOut = "";                               # Command to force timeout

&limitCpuTime (1);                              # Limit CPU-time per process
&limitWallClock (1);                            # Limit wall-clock per process
&limitFileSize (10000);                         # Limit size of files created
&limitHeapSize (100000);                        # Limit size of heap
&limitProcesses (1000);                         # Limit #processes

$|++;
print "\nEach test is either passed or failed; there is no partial credit.\n\n"
    . "To execute the test labelled IJ, type the command:\n"
#   . "     /c/cs$CLASS/Hwk${HWK}/Tests/$TEST\n"
    . "     $PROGRAM < /c/cs$CLASS/Hwk${HWK}/Tests/$TEST\n"
    . "The answer expected is in /c/cs$CLASS/Hwk${HWK}/Tests/$ANSWER.\n\n";

my $total = 0;

&header ('Generic');
$total += &runTest ('01', 'Code, string, & char constant; no comments');

&header ('ANSI Comments');
$total += &runTest ('02', 'Simple comments');
$total += &runTest ('03', 'Unterminated comment (graceful)', 'Graceful');
$total += &runTest ('04', 'Comments nest, not!');
$total += &runTest ('05', 'String or char constant within comment, not!');

&header ('Strings and char constants');
$total += &runTest ('06', 'Single quote in string');
$total += &runTest ('07', 'Escaped double quote is string');
$total += &runTest ('08', 'Adjacent strings');
$total += &runTest ('09', 'Comment followed/preceded by string');

&header ('Line splices');
$total += &runTest ('10', 'Simple');
$total += &runTest ('11', 'Within start-comment');
$total += &runTest ('12', 'Sequence of splices I');

&header ('Stress');
$total += &runTest ('13', 'Preprocessor directives (graceful)', 'Graceful');

print "\nEnd of Public Script\n\n";

&header ('Cxref');
$total += &runTest ('14', 'Alphanumeric identifiers');

&header ('ANSI & C++ Comments');
$total += &runTest ('15', 'Escaped * and / end comment, not!');
$total += &runTest ('16', 'Adjacent and "overlapping" comments');
$total += &runTest ('17', '/ or * precedes begin-comment');
$total += &runTest ('18', '/ or * precedes end-comment');
$total += &runTest ('19', '/* and */ cannot overlap');
$total += &runTest ('20', '*/ and /* cannot overlap');

&header ('Strings and Char Constants');
$total += &runTest ('21', 'Escaped escape is string');
$total += &runTest ('22', 'Escaped characters in string');
$total += &runTest ('23', '"Comment" within string');
$total += &runTest ('24', 'Empty string');
$total += &runTest ('25', 'Unterminated string (graceful)', 'Graceful');
$total += &runTest ('26', '/ precedes/follows single quote');
$total += &runTest ('27', 'Variations on char constants');
$total += &runTest ('28', 'Unterminated char constant (graceful)', 'Graceful');

&header ('Line Splices');
$total += &runTest ('29', 'Within end-comment');

# $total += &runTest ('30', 'Within escaped character in string');
$total += &runTest ('30a','Within escaped matching quote in string');
$total += &runTest ('30b','Within escaped escape in string');
$total += &runTest ('30c','Within other escaped character in string');

# $total += &runTest ('31', 'Within escaped character in char constant');
$total += &runTest ('31a','Within escaped matching quote in char constant');
$total += &runTest ('31b','Within escaped escape in char constant');
$total += &runTest ('31c','Within other escaped character in char constant');

$total += &runTest ('32', 'Sequence within start/end-comment');

# $total += &runTest ('33', 'Sequence in string or char constant');
$total += &runTest ('33a','Sequence in escaped matching quote');
$total += &runTest ('33b','Sequence in escaped escape');
$total += &runTest ('33c','Sequence in other escaped character');

$total += &runTest ('34', 'Nest, not!');
$total += &runTest ('35', 'In other unusual places');

&header ('Cxref');
$total += &runTest ('36', 'Alphabetic+underscore identifiers');
$total += &runTest ('37', 'Surrounding multiline comment');
$total += &runTest ('38', 'Line splice within hexadecimal constant');

&header ('Stress Tests');
$total += &runTest ('39', 'Very long');
$total += &runTest ('40', 'What else can precede/follow /');

print "\n $total points  Total for $NAME\n";

&header ('Non-Credit Tests');
$TEST   = "tIJ";
$total += &runTest ('41', 'Proper use of ungetchar()?');
$TEST   = "tIJ.c";

&header ("Possible Deductions (assessed later as appropriate) ");
&deduction ( -5, "Does not make");
&deduction ( -1, "Makefile missing");
&deduction ( -1, "Makefile incorrect");
&deduction ( -1, "Log file incorrectly named");
&deduction ( -1, "Log file lacks estimated time");
&deduction ( -1, "Log file lacks total time");
&deduction ( -1, "Log file lacks statement of major difficulties");
&deduction ( -1, "Compilation errors using -Wall -std=c99 -pedantic");

if ($TYPE eq "Final") {
   print "\n";
   system ("rm -f $PROGRAM *.o")                # Cleanup if final script
      unless ($LANG eq "Perl");
}

if ($TYPE eq "Public") {                        # Reminder to students
   system ("/c/cs$CLASS/bin/checklog -noprint");
   system ("/c/cs$CLASS/bin/checkmake -noprint")
      unless ($LANG eq "Perl");
}

exit $total;


##########
# Print section header for tests
sub sectionHeader {
   printf ("\n%11s%s\n", "", @_);
}


##########
# Print header for tests
sub header {
   printf ("\n%15s%s\n", "", @_);
}


##########
# Print addition
sub addition {
   my ($points, $title) = @_;
   printf ("%3d point       $title\n", $points);
   return $points;
}


##########
# Print deduction
sub deduction {
   my ($points, $title) = @_;
   printf ("%3d point       $title\n", $points);
   return $points;
}


##########
# Print possible deduction
sub possibleDeduction {
   printf ("%18d %s\n", @_);
}


##########
# Run a test
sub runTest {
   my ($test, $title, $conds) = @_;
   my $where   = "/c/cs$CLASS/Hwk$HWK/Tests";   # Where to find test files
   my $run     = "/c/cs$CLASS/bin/run";         # Utility to run program
   my $diff    = "/usr/bin/diff";               # Utility to compare files
   my $head    = "/c/cs$CLASS/bin/Head";        # Utility to output files
   my $results = "/tmp/$NAME.$$";               # Results of test
   my $diffs   = "/tmp/diff.$$";                # Expected results vs. results
   my $errors  = "/tmp/errs.$$";                # Error messages generated
   my ($status, @conds, $points);

   my $testFile = "$where/$TEST";               # Name of test file
   $testFile    =~ s{IJ}{$test};
   my $answers  = "$where/$ANSWER";             # Name of answer file
   $answers     =~ s{IJ}{$test};

   return 0                                     # Either execute all tests or
      unless (keys %WHICH == 0                  #   only those on command line
	      || exists $WHICH{$test});

   (-r $testFile)
      || die ("$0: missing test file $testFile\n");

   push @UNLINK, $results, $errors, $diffs;     # Files to delete on signal

   if (-x $testFile) {
      $status = execute ($testFile, undef, $results, $errors);
   } elsif ($LANG eq "Perl") {
      $status = execute ($PROGRAM, $testFile, $results, $errors);
   } else {
      $status = execute ("$run $PROGRAM", $testFile, $results, $errors);
   }

   if (defined $conds && $conds eq "Graceful") {
      @conds = ('NORMAL', $status);
   } else {
      (-r $answers)
	 || die ("$0: missing answer file $answers\n");
      system ("$diff $answers $results  > $diffs  2>> $errors");
      system ("$head $errors $diffs");
      @conds = ('NULL', $errors,  'NULL', $diffs);
   }

   if (defined $conds && $conds eq "Error message") {
      @conds = ('NONNULL', $errors,  'NULL', $diffs);
   }

   if (defined $conds && $conds =~ m{^Deduct=(\d+)$}) {
      @conds = ('DEDUCT', $1, @conds);
   }

   $points = &correct (@conds);
   if (exists $SCALE{$test}) {
      my $scale = $SCALE{$test};
      $points *= $scale;
      printf ("%4s point  %3s. %s  [%.1f]\n", $points, $test, $title, $scale);

   } else {
      printf ("%4s point  %3s. %s\n", $points, $test, $title);
   }

   system ("rm -f $results $errors $diffs");
   pop @UNLINK;  pop @UNLINK;  pop @UNLINK;     # Remove added files

   return $points;
}


##########
#  correct ({[UNOP FILE] | ['NORMAL' STATUS]}*)
#
#  Return 1 if the conjunction of the specified tests is true, else 0, where:
#
#    UNOP FILE (where UNOP is either 'NULL' or 'NONNULL'):
#      Is the size of the file FILE as specified?
#
#    'NORMAL' STATUS:
#      Did the process terminate normally?
#
#    'DEDUCT' POINTS:
#      Change the point values to 0 for success, -POINTS for failure
#
sub correct {
   my $op;
   my ($success, $failure) = (1, 0);

   while ($op = shift) {
      if ($op eq 'NULL') {
	 my $arg = shift;
	 print STDERR "$op $arg\n" if $debug;
	 if (-s $arg) {
	    if ($arg =~ m{/diff\.}) {
	       print "Error: STDOUT differs from expected\n";
	    } elsif ($arg =~ m{/errs\.}) {
	       print "Error: STDERR should be empty\n";
	    } else {
	       print "Error: File $arg is nonempty\n";
	    }
	    return $failure;
	 }

      } elsif ($op eq 'NONNULL') {
	 my $arg = shift;
	 print STDERR "$op $arg\n" if $debug;
	 if (!-s $arg) {
	    if ($arg =~ m{/errs\.}) {
	       print "Error: STDERR should be nonempty\n";
	    } else {
	       print "Error: File $arg is empty\n";
	    }
	    return $failure;
	 }

      } elsif ($op eq 'NORMAL') {
	 my $arg = 127 & shift;
	 print STDERR "$op $arg\n" if $debug;
	 if ($arg != 0) {
	    print "Error: Status = $arg is nonzero\n";
	    return $failure;
	 }

      } elsif ($op eq 'DEDUCT') {
	 my $arg = shift;
	 ($success, $failure) = (0, -$arg);
      }
   }
   return $success;
}


##########
# Create program to test
sub makeProgram {
#  system ("rm -f $PROGRAM");                   # Delete program & object files
#  system ("rm -f *.o")
#     if ($TYPE eq "Final");

   (-f "Makefile" || -f "makefile")             # Give warning if no Makefile
      || warn ("$0: no makefile found\n");

   system ("/c/cs223/bin/makewarn $PROGRAM");
   ($? == 0)
      || die ("$0: cannot compile $PROGRAM\n");

   for my $p (split (" ", $PROGRAM)) {
      -e $p
	 || die ("$0: could not make $p\n");
   }
}


##########
# Limit CPU-time, wall-clock-time, file-size, and/or heap-size
use BSD::Resource;

sub limitCpuTime { # (time in seconds)
   my ($soft, $hard) = @_;
   $hard = $soft
      if (! defined($hard));
   setrlimit (RLIMIT_CPU, $soft, $hard);
}

sub limitWallClock { # (time in seconds)
   my ($wall) = @_;                             # TERM after WALL seconds;
   $TimeOut = "timeout -k $wall $wall";         #   KILL after 2*WALL seconds
}

sub limitFileSize { # (size in kilobytes)
   my ($size) = @_;
   $size *= 1024;
   setrlimit (RLIMIT_FSIZE, $size, $size);
}

sub limitHeapSize { # (size in kilobytes        # Bug: Has no effect
   my ($size) = @_;
   $size *= 1024;
   setrlimit (RLIMIT_VMEM, $size, $size);
}

sub limitProcesses { # (#processes)             # Bug: Has no effect
   my ($nproc) = @_;
   setrlimit (RLIMIT_NPROC, $nproc, $nproc);
}


##########
# Execute program after redirecting stdin, stdout, & stderr and return status
sub execute {
   my ($program, $stdin, $stdout, $stderr) = @_;
   my ($pid, $status);

   (defined ($pid = fork))                      # Create child process
      || die ("$0: fork failed\n");

   if ($pid == 0) {                             # Child process
      open (STDIN, "cat $stdin |")              #  Redirect stdin
	 if (defined $stdin);
      open (STDOUT, ">$stdout")                 #  Redirect stdout
	 if (defined $stdout);
      open (STDERR, ">$stderr")                 #  Redirect stderr
	 if (defined $stderr);
      mkdir ($TMPDIR)                           #  Create a temporary directory
	 || die ("$0: mkdir $TMPDIR failed\n");
      system ("/bin/cp $PROGRAM $TMPDIR");      #    With a copy of the program
      chdir ("$TMPDIR")                         #    And cd there
	 || die ("$0: chdir $TMPDIR failed\n");
      (exec "$TimeOut $program")                #  Execute command
	 ||  die ("$0: exec failed\n");
   }

   waitpid ($pid, 0);                           # Wait for child to die,
   $status = $?;

   system ("/bin/chmod -R +w $TMPDIR");         # Delete temporary directory
   system ("/bin/rm -rf $TMPDIR");
   (! -e $TMPDIR)
      || die ("$0: cannot delete $TMPDIR\n");

   return $status;                              # ... and return exit status
}


##########
# $FILES is a blank-separated list of filenames which may include wildcards.
# If any of these files exist in the current working directory, print their
# names and return -1; else return 0.
sub localCopies { # ($files)
   my ($files) = @_;
   open (LS, "ls -l $files 2>/dev/null |")
       || die ("$0: cannot ls -l $files\n");
   my @ls = <LS>;
   close (LS);
   print @ls;
   return (@ls > 0) ? -2 : 0;
}
